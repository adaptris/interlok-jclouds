ext {
  componentName='Interlok JClouds/Blobstore'
  componentDesc="Use Apache jclouds to access your cloud storage"
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
  jcloudsVersion = '2.3.0'
}

dependencies {
  api project(":interlok-jclouds-common")
  api ("org.apache.jclouds:jclouds-blobstore:$jcloudsVersion")

  api ("javax.xml.bind:jaxb-api:2.3.1")
//  api ("jakarta.xml.bind:jakarta.xml.bind-api-parent:3.0.0")

  testImplementation "org.apache.jclouds.api:filesystem:$jcloudsVersion"
  testImplementation "org.apache.jclouds.provider:azureblob:$jcloudsVersion"
}


jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
              "Build-Jdk": System.getProperty('java.version'),
              "Implementation-Title": componentName,
              "Implementation-Version": project.version,
              "Implementation-Vendor-Id": project.group,
              "Implementation-Vendor": organizationName)
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
    classifier = 'examples'
    from new File(buildDir, '/examples')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.extensions.delombokTask
}

artifacts {
  archives javadocJar
  archives examplesJar
  archives sourcesJar
}


publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact javadocJar { classifier "javadoc" }
      artifact examplesJar { classifier "examples" }
      artifact sourcesJar { classifier "sources" }
      pom.withXml {
        asNode().appendNode("description", componentDesc)
        asNode().appendNode("name", componentName)
        def properties = asNode().appendNode("properties")
        properties.appendNode("target", "3.7.3+")
        properties.appendNode("tags", "s3,backblaze,azure blob,google cloud storage,rackspace")
        properties.appendNode("license", "false")
        properties.appendNode("externalUrl", "https://jclouds.apache.org/")
        properties.appendNode("readme", "https://github.com/adaptris/interlok-jclouds/raw/develop/README.md")
        properties.appendNode("repository", "https://github.com/adaptris/interlok-jclouds")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

delombok {
  target = delombokTargetDir
}

task deleteGeneratedFiles(type: Delete) {
  delete delombokTargetDir
}

clean.dependsOn deleteGeneratedFiles
javadoc.dependsOn offlinePackageList
